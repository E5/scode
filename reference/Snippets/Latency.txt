you can determine synth node latency by sending a bundle to the server
(helpfiles Server, bundledCommands). New synths are generated by the
EventStreamPlayer with server latency (get/set it with s.latency).

(
s = Server.local;
Server.default = s;
s.boot;
)

(
x = Synth.basicNew("default", s);
~specLatency = 1;

s.latency = ~specLatency;
p = Pbind(\midinote, 76).play;
s.sendBundle(~specLatency, x.newMsg);
)

(
x.free;
p.stop;
)

// If Pbind and Synth are from different SynthDefs with different envelope attack
// characteristics you may want to adjust with different latencies.

// set default again

s.latency = 0.2;

// Syncing with Quants for letting other Synths / EventStreamPlayers step in:

(
~specLatency = 0.7;

s.latency = ~specLatency;
x = Synth.basicNew("default", s);
p = Pbind(\midinote, 76, \dur, 0.5).play(quant: 0.5);

Task { s.sendBundle(~specLatency, x.newMsg;) }.play(quant: 0.5);
)

// step in

q = Pbind(\midinote, 79, \dur, 0.5).play(quant: 0.5);

(
y = Synth.basicNew("default", s);
Task { s.sendBundle(~specLatency, y.newMsg, y.setMsg(\freq, 1200, \amp, 0.2)) }.play(quant: 0.5);
)

(
[x,y].do(_.free);
[p,q].do(_.stop);
)

Greetings

Daniel

------------------------
www.daniel-mayer.at
------------------------
